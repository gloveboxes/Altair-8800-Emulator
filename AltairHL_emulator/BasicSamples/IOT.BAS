05 PRINT "============================================"
10 PRINT "OPEN WEATHER MAP IOT APP"
20 PRINT "CHANGE PAUSE TO VARY THE DELAY"
30 PAUSE = 500000

40 FOR I = 1 TO 1E+06
50 PRINT "============================================"
60 PRINT "Reading:";I

80 REM OUT PORT 32,2 PUBLISHES THE WEATHER AND GEO DATA TO AZURE IOT SERVICES
90 OUT 32,2

100 REM GET TEMPERATURE AS A STRING
110 PORT = 45
120 GOSUB 500
130 PRINT "Temperature: ";A$;" celsius"

135 REM GET PRESSURE AS A STRING
140 PORT = 46
150 GOSUB 500
160 PRINT "Pressure: ";A$;" millibars"

165 REM GET HUMIDITY AS A STRING
170 PORT = 47
180 GOSUB 500
190 PRINT "Humidity: ";A$;"%"

195 REM GET WEATHER OBSERVATION AS A STRING
200 PORT = 48
210 GOSUB 500
220 PRINT "Weather: ";A$

230 PRINT "Pause..."
240 FOR D = 1 TO PAUSE: NEXT D
250 NEXT I
260 STOP

500 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
510 A$ = ""
520 C=INP(PORT)
530 IF C = 0 THEN RETURN
540 A$ = A$ + CHR$(C)
550 GOTO 520
