100 WIDTH 150
200 PRINT "===================================================="
300 PRINT "OPEN WEATHER MAP IOT APP WITH JITTER"
400 SECONDS% = 10

500 WEATHERKEY = 34
600 WEATHERVALUE = 35
700 WEATHERITEMS = 6

800 LOCATIONKEY = 36
900 LOCATIONVALUE = 37
1000 LOCATIONITEMS = 4

1100 POLLUTIONKEY = 38
1200 POLLUTIONVALUE = 39
1300 POLLUTIONITEMS = 9

1400 PRINT "SEND WEATHER DATA TO IOT CENTRAL EVERY";SECONDS%;"SECONDS"
1500 PRINT "TEMPERATURE JITTERED RANDOMLY FOR ANOMALY DETECTION"

1600 REM GET RANDOM NUMBER SEED FROM PLATFORM
1700 PORT = 44 : GOSUB 4800 : RANDOMIZE VAL(RSTRING$)
1800 RJITKEY% = RND * 25: I = 0
1900 RCOUNT% = 0
2000 READING# = 0

2100 WHILE 1

2200 IF RCOUNT% MOD 51 = RJITKEY% THEN RJITKEY% = RND * 50 : RJITVAL% = (RND * 100) : RCOUNT% = 0 ELSE RJITVAL% = 0 : RCOUNT% = RCOUNT% + 1
2300 READING# = READING# + 1

2400 PRINT "============================================================================================================================"
2500 PORT = 43 : GOSUB 4800 : TIME$ = RSTRING$
2600 PRINT "Reading", "Jitter", "Jitter Value", "Time"
2700 PRINT READING#, RJITKEY%, RJITVAL%, TIME$

2800 ITEMKEY = WEATHERKEY : ITEMVALUE = WEATHERVALUE : ITEMS = WEATHERITEMS : GOSUB 3800
2900 PRINT

3000 ITEMKEY = POLLUTIONKEY : ITEMVALUE = POLLUTIONVALUE : ITEMS = POLLUTIONITEMS : GOSUB 3800
3100 PRINT

3200 ITEMKEY = LOCATIONKEY : ITEMVALUE = LOCATIONVALUE : ITEMS = LOCATIONITEMS : GOSUB 3800
3300 PRINT
3400 GOSUB 5900 : REM PUBLISH WEATHER DATA
3500 GOSUB 5500 : REM SLEEP
3600 WEND

3700 END
3800 REM PRINT ITEM KEY VALUE PAIRS
3900 PRINT
4000 FOR I = 1 TO ITEMS
4100 PORT = ITEMKEY : PDATA = I - 1 : GOSUB 4800 : PRINT RSTRING$, 
4200 NEXT I
4300 PRINT
4400 FOR I = 1 TO ITEMS
4500 PORT = ITEMVALUE : PDATA = I - 1 : GOSUB 4800 : PRINT RSTRING$, 
4600 NEXT I
4700 RETURN

4800 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
4900 OUT PORT, PDATA
5000 RSTRING$ = ""
5100 C=INP(200)
5200 IF C = 0 THEN RETURN
5300 RSTRING$ = RSTRING$ + CHR$(C)
5400 GOTO 5100

5500 PRINT "" : PRINT "Pause";SECONDS%;"seconds."
5600 OUT 30, SECONDS%
5700 IF INP(30) = 1 THEN GOTO 5700
5800 RETURN

5900 REM PUBLISH WEATHER DATA
6000 REM WAIT FOR PUBLISH WEATHER PENDING TO GO FALSE
6100 WAIT 32, 1, 1
6200 REM PUBLISH TO IOT CENTRAL WITH JITTER
6300 OUT 32, RJITVAL%
6400 RETURN

