10 WIDTH 150
100 PRINT "===================================================="
200 PRINT "OPEN WEATHER MAP IOT APP WITH JITTER"

300 SECONDS% = 20
400 PRINT "SEND WEATHER DATA TO IOT CENTRAL EVERY";SECONDS%;"SECONDS"
500 PRINT "TEMPERATURE JITTERED RANDOMLY FOR ANOMALY DETECTION"
600 REM GET RANDOM NUMBER SEED FROM PLATFORM

700 PORT = 44 : GOSUB 5900 : RANDOMIZE VAL(RSTRING$)
800 RJITKEY% = RND * 25: I = 0
900 RCOUNT% = 0

1000 WHILE 1
1100 PRINT "============================================================================================================================"
1200 PORT = 42 : GOSUB 5900 : TIME$ = RSTRING$

1210 PRINT "Reading", "Jitter", "Time (UTC)"
1230 PRINT RCOUNT%, RJITKEY%, TIME$
1300 IF RCOUNT% MOD 51 = RJITKEY% THEN RJITKEY% = RND * 50 : RJITVAL% = (RND * 100) : RCOUNT% = 0 ELSE RJITVAL% = 0 : RCOUNT% = RCOUNT% + 1

1400 REM GET TIME AS STRING

1700 PRINT ""

1800 REM GET TEMPERATURE AS A STRING
1900 PORT = 45 : GOSUB 5900 : TEMPERATURE$ = STR$(VAL(RSTRING$) + RJITVAL%)
2000 IF RJITVAL% = 0 THEN TITLE$ = "Celisius" ELSE TITLE$ = "JITTER"

2200 PORT = 46 : GOSUB 5900 : PRESSURE$ = RSTRING$
2500 PORT = 47 : GOSUB 5900 : HUMIDITY$ = RSTRING$
2800 PORT = 48 : GOSUB 5900 : DESCRIPTION$ = RSTRING$
3100 PORT = 53 : GOSUB 5900 : WINDSPEED$ = RSTRING$
3400 PORT = 54 : GOSUB 5900 : WINDDIRECTION$ = RSTRING$

3530 PRINT TITLE$, "Millibar", "Humidity %", "Wind km/h", "Wind degrees", "Description"
3535 PRINT TEMPERATURE$, PRESSURE$, HUMIDITY$, WINDSPEED$, WINDDIRECTION$, DESCRIPTION$

3550 PRINT ""
3700 PORT = 60 : GOSUB 5900:  AQI$ = RSTRING$
3800 PORT = 61 : GOSUB 5900 : CO$ = RSTRING$
3810 PORT = 62 : GOSUB 5900 : NO$ = RSTRING$
3820 PORT = 63 : GOSUB 5900 : NO2$ = RSTRING$
3830 PORT = 64 : GOSUB 5900 : O3$ = RSTRING$
3840 PORT = 65 : GOSUB 5900 : SO2$ = RSTRING$
3850 PORT = 66 : GOSUB 5900 : NH3$ = RSTRING$
3860 PORT = 67 : GOSUB 5900 : PM25$ = RSTRING$
3870 PORT = 68 : GOSUB 5900 : PM10$ = RSTRING$

3880 PRINT "AQI", "CO", "NO", "NO2", "O3", "SO2", "NH3", "PM2.5", "PM1.0"
3885 PRINT AQI$, CO$, NO$, NO2$, O3$, SO2$, NH3$, PM25$, PM10$

4200 PRINT ""
4300 PORT = 57 : GOSUB 5900 : CITY$ = RSTRING$
4500 PORT = 55 : GOSUB 5900 : COUNTRY$ = RSTRING$
4800 PORT = 49 : GOSUB 5900 : LATITUDE$ = RSTRING$
5100 PORT = 50 : GOSUB 5900 : LONGITUDE$ = RSTRING$

5220 PRINT "Latitude", "Longitide", "Place"
5230 PRINT LATITUDE$, LONGITUDE$, CITY$ + ", " + COUNTRY$

5300 REM PUBLISH TO IOT CENTRAL WITH JITTER
5400 OUT 32, RJITVAL%

5600 GOSUB 6500

5700 WEND
5800 END

5900 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
6000 RSTRING$ = ""
6100 C=INP(PORT)
6200 IF C = 0 THEN RETURN
6300 RSTRING$ = RSTRING$ + CHR$(C)
6400 GOTO 6100

6500 PRINT "" : PRINT "Pause";SECONDS%;"seconds."
6600 OUT 30, SECONDS%
6700 IF INP(30) = 1 THEN GOTO 6700
6800 RETURN

