10 PRINT
20 PRINT "Open Weather Map IoT App"
30 REM FETCH THE LOCATION AND WEATHER DATA
40 FOR I = 1 TO 1E+06
50 PRINT "============================================"
60 PRINT "Reading:";I
65 REM OUT PORT 32,1 GETS GEOLOCATION AND WEATHER FOR THE GEOLOCATION
70 OUT 32,1
80 REM OUT PORT 32,2 PUBLISHES THE WEATHER AND GEO DATA TO AZURE IOT SERVICES
90 OUT 32,2
100 REM GET THE TEMPERATURE AS A STRING
110 PORT = 45
120 GOSUB 500
130 PRINT "Temperature: ";A$;" celsius"
140 PORT = 46
150 GOSUB 500
160 PRINT "Air pressure: ";A$;" millibars"
170 PORT = 47
180 GOSUB 500
190 PRINT "Humidity: ";A$;"%"
200 PORT = 48
210 GOSUB 500
220 PRINT "Weather: ";A$
230 REM 1000000 ITERATIONS IS APPROX 6 SECONDS DELAY WHEN ALTAIR BUILT IN RELEASE MODE
240 FOR J = 1 TO 1E+06: NEXT J
250 NEXT I
300 RETURN
500 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
510 A$ = ""
520 C=INP(PORT)
530 IF C = 0 THEN RETURN
540 A$ = A$ + CHR$(C)
550 GOTO 520
