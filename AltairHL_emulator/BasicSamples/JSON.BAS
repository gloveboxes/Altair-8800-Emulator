100 WIDTH 150
150 PRINT "===================================================="
200 PRINT "OPEN WEATHER MAP IOT APP"
250 DELAY% = 10

260 WEATHERPORT = 35
270 LOCATIONPORT = 37
280 POLLUTIONPORT = 39

300 PRINT "SEND WEATHER DATA TO IOT CENTRAL EVERY";DELAY%;"SECONDS"
350 RCOUNT% = 0

400 WHILE 1
450 PRINT "===================================================="
500 PRINT "Reading:"; RCOUNT%; ", BASIC free memory:"; FRE(0)
550 RCOUNT% = RCOUNT% + 1

600 REM GET TIME AS STRING
650 PORT = 43 : GOSUB 2700 : PRINT "Time (local): ";RSTRING$

800 PORT = WEATHERPORT : PDATA = 0 : GOSUB 2700 : TEMPERATURE$ = RSTRING$
810 PORT = WEATHERPORT : PDATA = 1 : GOSUB 2700 : PRESSURE$ = RSTRING$
815 PORT = WEATHERPORT : PDATA = 2 : GOSUB 2700 : HUMIDITY$ = RSTRING$
825 PORT = POLLUTIONPORT : PDATA = 0 : GOSUB 2700 : AIRQUALITYINDEX$ = RSTRING$
830 PORT = LOCATIONPORT : PDATA = 0 : GOSUB 2700 : LATITUDE$ = RSTRING$
835 PORT = LOCATIONPORT : PDATA = 1 : GOSUB 2700 : LONGITUDE$ = RSTRING$

900 PRINT : PRINT "Celsius", "Millibars", "Humidity %", "AQI (CAQI)", "Latitude", "Longitude"
910 PRINT TEMPERATURE$, PRESSURE$, HUMIDITY$, AIRQUALITYINDEX$, LATITUDE$, LONGITUDE$
920 PRINT

2000 GOSUB 2250 : REM Generate JSON
2010 GOSUB 3300 : REM Publish JSON

2050 PRINT: PRINT "Pause";DELAY%;"seconds."
2100 GOSUB 3000

2150 WEND
2200 END

2250 REM BUILD JSON STRING
2300 RJSON$ = "{"
2350 RJSON$ = RJSON$ + CHR$(34) + "temperature" + CHR$(34) + ":" + TEMPERATURE$ + ","
2400 RJSON$ = RJSON$ + CHR$(34) + "pressure" + CHR$(34) + ":" +  PRESSURE$ + ","
2450 RJSON$ = RJSON$ + CHR$(34) + "humidity" + CHR$(34) + ":" +  HUMIDITY$ + ","
2500 RJSON$ = RJSON$ + CHR$(34) + "latitude" + CHR$(34) + ":" +  LATITUDE$ + ","
2550 RJSON$ = RJSON$ + CHR$(34) + "longitude" + CHR$(34) + ":" +  LONGITUDE$ + ","
2560 RJSON$ = RJSON$ + CHR$(34) + "aqi" + CHR$(34) + ":" +  AIRQUALITYINDEX$
2600 RJSON$ = RJSON$ + "}"
2650 RETURN

2700 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
2710 OUT PORT, PDATA
2750 RSTRING$ = ""
2800 C=INP(200)
2850 IF C = 0 THEN RETURN
2900 RSTRING$ = RSTRING$ + CHR$(C)
2950 GOTO 2800

3000 REM SUBROUTINE DELAYS PROGRAM EXECUTION BY DELAY% SECONDS
3100 OUT 30, DELAY%
3150 IF INP(30) = 1 THEN GOTO 3150
3250 RETURN

3300 REM SUBROUTINE PUBLISHES JSON TO AZURE IOT
3210 WAIT 31, 1, 1 : REM WAIT FOR PUBLISH JSON PENDING TO GO FALSE
3350 LENGTH% = LEN(RJSON$)
3400 IF LENGTH% = 0 THEN RETURN
3450 IF LENGTH% > 256 THEN RETURN
3500 PRINT "PUBLISHING JSON TO AZURE IOT"
3550 PRINT RJSON$
3600 FOR DATAINDEX% = 1 TO LENGTH%
3650 OUT 31, ASC(MID$(RJSON$, DATAINDEX%, 1))
3700 NEXT DATAINDEX%
3750 OUT 31, 0 : REM TERMINATING NULL CAUSE PUBLISH TO AZURE IOT
3800 RETURN

